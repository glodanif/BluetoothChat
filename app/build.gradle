def final VERSION_CODE = 32
def final VERSION_NAME = '1.2.10'

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {

    compileSdkVersion 28

    defaultConfig {
        applicationId "com.glodanif.bluetoothchat"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode VERSION_CODE
        versionName VERSION_NAME
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    kotlin {
        experimental {
            coroutines 'enable'
        }
    }

    signingConfigs {

        release {

            if (project.hasProperty("bluetoothchat.properties") &&
                    file(project.property("bluetoothchat.properties")).exists()) {

                Properties properties = new Properties()
                properties.load(
                        new FileInputStream(file(project.property("bluetoothchat.properties"))))

                storeFile file(properties['keystore'])
                storePassword properties['keystore.password']
                keyAlias properties['keyAlias']
                keyPassword properties['keyPassword']
            } else {
                null
            }
        }
    }

    buildTypes {

        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.dev'
            versionNameSuffix '-DEV'
            buildConfigField "boolean", "AUTORESPONDER", "false"
        }

        autoresponder {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.ar'
            versionNameSuffix '-AR'
            buildConfigField "boolean", "AUTORESPONDER", "true"
        }

        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "AUTORESPONDER", "false"
        }
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
    }
}

dependencies {

    def final SUPPORT_VERSION = '28.0.0-rc01'
    def final ROOM_VERSION = '1.1.1'
    def final LIFECYCLE_VERSION = '1.1.1'
    def final LEAK_CANARY_VERSION = '1.6.1'
    def final ESPRESSO_VERSION = '3.0.0'
    def final COROUTINES_VERSION = '0.23.3'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    implementation "com.android.support:design:$SUPPORT_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_VERSION"

    implementation 'com.android.support.constraint:constraint-layout:2.0.0-alpha2'

    implementation "android.arch.persistence.room:runtime:$ROOM_VERSION"
    kapt "android.arch.persistence.room:compiler:$ROOM_VERSION"

    implementation "android.arch.lifecycle:runtime:$LIFECYCLE_VERSION"
    implementation "android.arch.lifecycle:extensions:$LIFECYCLE_VERSION"
    kapt "android.arch.lifecycle:compiler:$LIFECYCLE_VERSION"

    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
    implementation 'com.github.jkwiecien:EasyImage:2.0.4'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'me.priyesh:chroma:1.0.2'
    implementation 'io.github.kobakei:ratethisapp:1.2.0'

    implementation 'org.koin:koin-android:1.0.0-beta-4'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$KOTLIN_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$COROUTINES_VERSION"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$COROUTINES_VERSION"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_VERSION"
    autoresponderImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$KOTLIN_VERSION"
    testImplementation 'io.mockk:mockk:1.8.6'
    testImplementation 'junit:junit:4.12'

    androidTestImplementation "com.android.support:support-annotations:$SUPPORT_VERSION"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION"
}

task assembleReleaseAndOpen << {
    Runtime.getRuntime().exec("explorer.exe /select,${buildDir.getAbsolutePath()}\\outputs\\apk\\release\\app-release.apk")
}

assembleReleaseAndOpen.dependsOn 'assembleRelease'
